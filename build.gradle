def projectApplicationId = 'com.sudox.messenger.android'
def projectBuildToolsVersion = '29.0.0'
def compileSdkProjectVersion = 29
def minSdkProjectVersion = 21
def versionMajor = 0
def versionMinor = 6
def versionUpdate = 0
def versionPatch = 0

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:3.4.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41")
        classpath("org.jetbrains.kotlin:kotlin-android-extensions:1.3.41")
        classpath("com.tencent.mm:AndResGuard-gradle-plugin:1.2.16")
    }
}

apply from: 'configs/vcs.gradle'

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url "https://bitbucket.org/sudox-inc/srepo-maven/raw/HEAD/"
            credentials {
                username BITBUCKET_LOGIN
                password BITBUCKET_PASSWORD
            }

            authentication{
                basic(BasicAuthentication)
            }
        }
    }
}

subprojects { subproject ->
    if (subproject.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion compileSdkProjectVersion
        buildToolsVersion projectBuildToolsVersion

        defaultConfig {
            minSdkVersion minSdkProjectVersion
            targetSdkVersion compileSdkProjectVersion
            versionCode versionMajor * 1000 + versionMinor * 100 + versionUpdate * 10 + versionPatch
            versionName "${versionMajor}.${versionMinor}.${versionUpdate}.${versionPatch}"
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

            if (name == 'app') {
                applicationId projectApplicationId
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}