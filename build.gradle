def versionMajor = 0
def versionMinor = 5
def versionUpdate = 0
def versionPatch = 2

// Build params
def projectApplicationId = 'com.sudox.messenger.android'
def projectBuildToolsVersion = '28.0.3'
def compileSdkProjectVersion = 28
def minSdkProjectVersion = 21

buildscript {
    ext.kotlinVersion = '1.3.31'

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0-RC14"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url "https://bitbucket.org/sudox-inc/srepo-maven/raw/HEAD/"
            credentials {
                username System.getProperty("BITBUCKET_LOGIN")
                password System.getProperty("BITBUCKET_PASSWORD")
            }

            authentication{
                basic(BasicAuthentication)
            }
        }
    }

    apply from: "$rootDir/detekt.gradle"
}

subprojects { subproject ->
    if (subproject.name != 'app') {
        apply plugin: 'com.android.library'
    } else {
        apply plugin: 'com.android.application'
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-platform-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion compileSdkProjectVersion
        buildToolsVersion projectBuildToolsVersion

        defaultConfig {
            minSdkVersion minSdkProjectVersion
            targetSdkVersion compileSdkProjectVersion
            versionCode versionMajor * 1000 + versionMinor * 100 + versionUpdate * 10 + versionPatch
            versionName "${versionMajor}.${versionMinor}.${versionUpdate}.${versionPatch}"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

            if (name == 'app') {
                applicationId projectApplicationId
            }
        }

        lintOptions {
            checkReleaseBuilds false
            abortOnError false
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'

        implementation 'androidx.annotation:annotation:1.1.0'
        implementation 'androidx.appcompat:appcompat:1.0.2'
        implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
        implementation "androidx.lifecycle:lifecycle-viewmodel:2.0.0"
        implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
        implementation 'androidx.legacy:legacy-support-v4:1.0.0'

        // For testing
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.mockito:mockito-core:2.25.1'
        testImplementation 'org.powermock:powermock-module-junit4:2.0.0-RC.1'
        testImplementation 'org.powermock:powermock-api-mockito2:2.0.0'
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
        androidTestImplementation 'junit:junit:4.12'
        androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
        androidTestImplementation 'org.mockito:mockito-core:2.25.1'
        androidTestImplementation 'org.mockito:mockito-android:2.25.1'
        androidTestImplementation 'androidx.test:runner:1.2.0'
        androidTestImplementation 'androidx.test:rules:1.2.0'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
        androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    }

    kapt {
        mapDiagnosticLocations true
        useBuildCache true
        generateStubs true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}