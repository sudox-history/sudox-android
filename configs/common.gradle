def versionMajor = 0
def versionMinor = 6
def versionUpdate = 0
def versionPatch = 0

ext.registerModule = { isApp, needExtensions, needApt, needKotlin ->
    if (isApp) {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    if (needKotlin) {
        apply plugin: 'kotlin-android'

        if (needExtensions) {
            apply plugin: 'kotlin-android-extensions'
        }

        if (needApt) {
            apply plugin: 'kotlin-kapt'
        }
    }

    android {
        buildToolsVersion "29.0.3"
        compileSdkVersion 29

        packagingOptions {
            exclude("META-INF/*.kotlin_module")
        }

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 29
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
            versionCode versionMajor * 1000 + versionMinor * 100 + versionUpdate * 10 + versionPatch
            versionName "${versionMajor}.${versionMinor}.${versionUpdate}.${versionPatch}"

            if (isApp) {
                applicationId 'ru.sudox.android'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }

        dexOptions {
            preDexLibraries true
        }
    }

    dependencies {
        if (needKotlin) {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.61'
        }
    }
}