def versionMajor = 0
def versionMinor = 6
def versionUpdate = 0
def versionPatch = 0

ext.registerModule = { isApp, needApt, needKotlin ->
    if (isApp) {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    if (needKotlin) {
        apply plugin: 'kotlin-android'

        if (needApt) {
            apply plugin: 'kotlin-kapt'
        }
    }

    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    android {
        buildToolsVersion "29.0.3"
        compileSdkVersion 29

        packagingOptions {
            exclude("META-INF/*.kotlin_module")
        }

        defaultConfig {
            minSdkVersion 23
            targetSdkVersion 29
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
            versionCode versionMajor * 1000 + versionMinor * 100 + versionUpdate * 10 + versionPatch
            versionName "${versionMajor}.${versionMinor}.${versionUpdate}.${versionPatch}"

            if (isApp) {
                applicationId 'ru.sudox.android'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        if (needKotlin) {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }

        dexOptions {
            preDexLibraries true
        }
    }

    dependencies {
        if (needKotlin) {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.71'
        }
    }
}